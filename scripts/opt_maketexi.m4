dnl
dnl Macro definitions for use in generating doc/M5manual.texi
dnl to convert option and section declarations
dnl into the m5run reference chapter and nodes
dnl
dnl -----------------------------------------
dnl  utilities
dnl -----------------------------------------
m4_define([_M5_BEGIN_TABLE],
[m4_ifdef([_M5_IN_TABLE],[],
[m4_define([_M5_IN_TABLE])dnl
[@table @option]
])])dnl
dnl
m4_define([_M5_END_TABLE],
[m4_ifdef([_M5_IN_TABLE],
[m4_undefine([_M5_IN_TABLE])dnl
[@end table]
],[])])dnl
dnl
m4_define([M5_PARAM],
[m4_translit([$1],[<>],[])])dnl
dnl -----------------------------------------
dnl  section declarations
dnl -----------------------------------------
dnl
dnl M5_HELP_SECTION([Header])
dnl   start of a new section 'Header Options'
dnl
m4_define([M5_HELP_SECTION],[dnl
_M5_END_TABLE()dnl
[
@node $1 Options
@section $1 Options
]])dnl
dnl
dnl -----------------------------------------
dnl  option declarations
dnl -----------------------------------------
dnl
dnl M5_OPTION_INFOBOOLEAN([h],[help],[msg],[?],[do_it])
dnl   informational boolean,
dnl     normally false
dnl     ANY mention (any of +h,-h,-?,--help)
dnl       means true, do the help thing, and exit
dnl     no mention means false, do nothing, run normally
dnl
m4_define([M5_OPTION_INFOBOOLEAN],
[_M5_BEGIN_TABLE()dnl
[@item --$2]m4_ifval([$4],[
[@itemx -$4]],[])m4_ifval([$1],[
[@itemx +$1
@itemx -$1]],[])])dnl
dnl
dnl M5_OPTION_BOOLEAN([s],[long],[helpmsg],[default],[do_it])
dnl   regular boolean, +s means true, -s means false
dnl   do_it=: or do_it=false, depending
dnl
m4_define([M5_OPTION_BOOLEAN],
[_M5_BEGIN_TABLE()dnl
[@item -[-no]-$2]
m4_ifval([$1],[[@itemx +$1 (-$1)]],[])])dnl
dnl
dnl  M5_OPTION_VALUE([s],[long],[pname],[helpmsg],[default],[var])
dnl    regular value option
dnl      '--long=value' or '-s value'  sets arbitrary string value
dnl
m4_define([M5_OPTION_VALUE],
[_M5_BEGIN_TABLE()dnl
[@item --]$2[=@var{]M5_PARAM([$3])[}]m4_ifval([$1],[[
@itemx -$1 @var{]M5_PARAM([$3])[}]],[])])dnl
dnl
dnl  M5_OPTION_NEGVALUE([s],[long],[pname],[helpmsg],[default],[var])
dnl    negatable value option
dnl      '--long=value'  sets it to  'value'
dnl      '-s value'      sets it to  'value'
dnl      '--no-long'     sets it to  '' (empty string)
dnl
m4_define([M5_OPTION_NEGVALUE],
[_M5_BEGIN_TABLE()dnl
[@item --]$2[=@var{]M5_PARAM([$3])[}]m4_ifval([$1],[[
@itemx -$1 @var{]M5_PARAM([$3])[}]],[])[
@itemx --no-]$2[]])dnl
dnl
dnl  M5_OPTION_PUSH([s],[long],[pname],[helpmsg],[|default],[push])
dnl    option that can be invoked multiple times
dnl    to push values onto a list
dnl
m4_define([M5_OPTION_PUSH],
[_M5_BEGIN_TABLE()dnl
[@item --]$2[=@var{]M5_PARAM([$3])[}]m4_ifval([$1],[[
@itemx -$1 @var{]M5_PARAM([$3])[}]],[])])dnl
dnl
dnl  M5_OPTION_END([--],[argsname],[helpmsg],[code])
dnl    -- indicates end of options
dnl    and remaining arguments are getting swept up
dnl
m4_define([M5_OPTION_END],
[_M5_BEGIN_TABLE()dnl
[@item $1 @var{]M5_PARAM([$2])[@dots{}}]])dnl
dnl
dnl  generate texinfo
dnl
m4_divert_text([],
[@c  Automatically generated by scripts/opttexi.m4 from
@c      scripts/options.m4
@c  You will want to do your editing there, since
@c  any changes made here will get overwritten.
@c]m4_dquote(m4_include([options.m4]))[]dnl
_M5_END_TABLE[][@c
@c end of generated content])dnl
